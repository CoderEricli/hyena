
plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
}

apply plugin: 'idea'
apply plugin: 'eclipse'


ext {
    springBootVersion = "2.1.4.RELEASE"
    mybatisStarterVersion = '2.0.1'
    swaggerVersion = '2.9.2'
}


allprojects {
    group = 'io.github.alphajiang'
    version = '0.0.4-SNAPSHOT'
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10

    compileJava {
        options.encoding = "UTF-8"
    }


    test.onlyIf { ! Boolean.getBoolean('skipTests') }

    repositories {
        maven { url 'http://maven.aliyun.com/mvn/repository/' }
        jcenter()
    }
    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR1"
        }
    }

}



subprojects {

    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'io.spring.dependency-management'


    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }


    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'hyena'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Micro service for user score hosting. '
                    url 'https://github.com/alphajiang/hyena'

                    scm {
                        connection 'https://github.com/alphajiang/hyena.git'
                        developerConnection 'https://github.com/alphajiang/hyena.git'
                        url 'https://github.com/alphajiang/hyena.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'alphajiang'
                            name 'Alpha Jiang'
                            email 'alphajiang@163.com'
                        }
                    }
                }
            }
        }
    }

    dependencies {



        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}




